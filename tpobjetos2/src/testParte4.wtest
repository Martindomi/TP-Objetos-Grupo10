import artefactos.*
import hechizos.*
import hechiceros.*

describe "test parte 4"{
	
	const alacachula = new Logo ("alacachula cachicomula",1) 
	const libroDeHechizosConAlacachula = new LibroDeHechizos ([alacachula, hechizoBasico])  
	const espejoFantastico = new EspejoFantastico ()
	const armaduraConBendicion = new Armadura(2, bendicion) 
	const unHechizo = new Hechizo(alacachula)
	const armaduraConAlacachula = new Armadura(2, unHechizo) 
	const espadaDelDestino = new ArmaBlanca ()
	const armadura = new Armadura (2,null) 
	const cota3 = new CotaDeMalla(5)
	const otraArmadura = new Armadura(2,cota3) 
	const collarDivino = new CollarDivino(5) 
    const xenia = new Hechicero(alacachula,[],100)
   	const thor = new Hechicero(hechizoBasico,[],100)
    const loki = new Hechicero(hechizoBasico,[],5)
 
    
  
    test "Conocer el precio de lista de un hechizo 
    de logo cuyo nombre es “alacachula cachicomula" {
    	
    	assert.equals(22, alacachula.costo())
    }
    
    test "Xenia canjea el hechizo por un hechizo básico.
     Saber cuántas monedas le quedan" {
     	
     	xenia.canjeHechizo(hechizoBasico)
     	assert.equals(100, xenia.oro())
     }
     
     test "Thor canjea el hechizo por el hechizo de logos “alacachula cachicomula” 
     del juego de datos. Cuántas monedas le quedan a Thor." {
     	
     	thor.canjeHechizo(alacachula)
     	assert.equals(83, thor.oro())
     }
     
     test "Hacer canjear dos veces a Thor el mismo hechizo básico." {
     	2.times{n => thor.canjeHechizo(hechizoBasico)}
     	assert.equals(90, thor.oro())
     }
     
     test "Loki quiere canjear el hechizo por el de “alacachula cachicomula” del juego de datos." {
     	assert.throwsExceptionWithMessage("no tienes suficiente oro para comprar este hechizo",
     	{loki.canjeHechizo(alacachula)})
     }
     
     test "Xenia compra una espada, un collar divino (de 5 perlas), 
     una armadura sin refuerzo y otra armadura con malla de 
     cota cuyo valor de lucha es 5. Cuántas monedas le quedan a Xenia." {
     	xenia.comprarArtefacto(espadaDelDestino)
     	xenia.comprarArtefacto(collarDivino)
     	xenia.comprarArtefacto(armadura)
     	xenia.comprarArtefacto(otraArmadura)
     	assert.equals(70.5, xenia.oro())
     	
     }
     
     test "2 Xenia compra una espada, un collar divino (de 5 perlas), 
     una armadura sin refuerzo y otra armadura con malla de 
     cota cuyo valor de lucha es 5. Cuántas monedas le quedan a Xenia." {
     	xenia.comprarArtefacto(espadaDelDestino)
     	xenia.comprarArtefacto(collarDivino)
     	xenia.comprarArtefacto(armadura)
     	xenia.comprarArtefacto(otraArmadura)
     	assert.equals(4, xenia.cantidadArtefactos())
     	
     }
     
     test "Thor compra una armadura con bendición como refuerzo, y
     una armadura reforzada con el hechizo alacachula (del juego de datos).
     Cuántas monedas le quedan a Thor." {
     
     	thor.comprarArtefacto(armaduraConBendicion)
     	thor.comprarArtefacto(armaduraConAlacachula)
     	assert.equals(74, thor.oro())
     }
     test "Loki quiere comprar el espejo"{
     	assert.throwsExceptionWithMessage("no tienes suficiente oro para comprar este artefacto",
     	{loki.comprarArtefacto(espejoFantastico)}
     	)
     }
     
     test "Xenia canjea un libro de hechizos que contiene el hechizo de logos de nombre 
     “alacachula cachicomula” y 
     el hechizo básico. Cuántas monedas le quedan a Xenia."{
     	xenia.canjeHechizo(libroDeHechizosConAlacachula)
     	assert.equals(69, xenia.oro())
     }
}